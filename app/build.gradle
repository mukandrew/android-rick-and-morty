import dependencies.Dependencies
import dependencies.Version

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: Dependencies.common

android {
    compileSdk Version.App.compileSdk

    defaultConfig {
        applicationId "academy.mukandrew.android.rickandmortycatalog"
        minSdk Version.App.minSdk
        targetSdk Version.App.targetSdk
        versionCode Version.App.versionCode
        versionName Version.App.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "BASE_URL", "\"${GRAPHQL_SERVER_ENDPOINT}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Version.Compose.general
        kotlinCompilerVersion Version.ClassPaths.kotlin
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // modules
    implementation project(Dependencies.Modules.presentation)
    implementation project(Dependencies.Modules.domain)
    implementation project(Dependencies.Modules.data)
    implementation project(Dependencies.Modules.remote)
    implementation project(Dependencies.Modules.cache)

    // android
    implementation Dependencies.Android.appcompat
    implementation Dependencies.Android.material

    // ktx
    implementation Dependencies.Ktx.core
    implementation Dependencies.Ktx.viewmodel
    implementation Dependencies.Ktx.fragment
    implementation Dependencies.Ktx.lifecycle

    // compose
    implementation Dependencies.Compose.ui
    implementation Dependencies.Compose.material
    implementation Dependencies.Compose.tooling
    implementation Dependencies.Compose.activity

    // hilt
    implementation Dependencies.Hilt.android
    implementation Dependencies.Hilt.lifecycle
    kapt Dependencies.Hilt.androidCompiler
    kapt Dependencies.Hilt.lifecycleCompiler

    // android test
    androidTestImplementation Dependencies.Test.androidJUnit
    androidTestImplementation Dependencies.Test.androidEspresso
    androidTestImplementation Dependencies.Test.androidCompose

    // debug
    debugImplementation Dependencies.Test.debugCompose
}
